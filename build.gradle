apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'idea'

jar {
    baseName = 'checkthat'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'checkthat.Application'

repositories {
    mavenCentral()
}
dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.codehaus.groovy:groovy'
    compile 'org.apache.httpcomponents:httpclient:4.+'
    compile 'org.apache.commons:commons-lang3:3.+'
    compile 'io.springfox:springfox-swagger-ui:2.+'
    compile 'io.springfox:springfox-swagger2:2.+'
}

bootRun {
    systemProperties = System.properties
}

run {
    systemProperties = System.properties
}

task generateProcfile << {
    File procfile = file('Procfile')
    procfile.text = "web: java -Dserver.port=\$PORT -jar ${relativePath(jar.archivePath)}"
}

task stage {
    dependsOn assemble, installDist, generateProcfile
}

task cleanAll {
    dependsOn clean, cleanIdea, cleanIdeaWorkspace
}
cleanAll << {
    delete 'out'
    delete fileTree('.').include('*.log')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.+')
    }
}
